buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'commons-io:commons-io:2.4'
  }
}

plugins {
    id 'nu.studer.plugindev' version '1.0.3'
    id "com.jfrog.bintray" version "1.2"
    id "com.ullink.msbuild" version "2.5"
    id "com.ullink.nuget" version "2.5"
}

group = 'com.ullink.gradle'
version = '2.5'

apply plugin: 'groovy'

description 'Gradle plugin for MSBuild project build.'

ext.dotnetPath = file('src/dotnet')
ext.mono = org.gradle.internal.os.OperatingSystem.current().unix
ext.parserBinary = mono ? 'ProjectFileParser.exe' : 'ProjectFileParser_Win.exe'

dependencies {
    testCompile 'junit:junit:4.11'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '0.7-groovy-2.0', transitive: false
    compile 'net.java.dev.jna:jna:3.5.0'
    compile 'net.java.dev.jna:platform:3.5.0'
    compile 'com.google.guava:guava:16.0.1'
}

msbuild.dependsOn nugetRestore
msbuild {
    solutionFile = file('src/dotnet/ProjectFileParser.sln')
    configuration = 'Release'
    projectName = 'ProjectFileParser'
}

task generateExe(dependsOn: msbuild, type: Exec) {
    workingDir = msbuild.mainProject.getProjectPropertyPath('OutputPath')
    commandLine = (mono ? ['mono']:[]) + [new File(dotnetPath, 'packages/ILRepack.1.26.0/tools/ILRepack.exe'),
                   '/log', '/wildcards', '/internalize', '/ndebug',
                   '/out:'+file("src/main/resources/META-INF/bin/$parserBinary"),
                   msbuild.mainProject.properties.TargetPath,
                   'Newtonsoft.Json.dll']
}

nugetRestore {
    solutionFile = file('src/dotnet/ProjectFileParser.sln')
}

bintray {
    // to remove when upgraded plugindev to 1.0.4
    user project.properties.bintrayUser
    key project.properties.bintrayApiKey
    pkg.repo = 'gradle-plugins'
    pkg.userOrg = 'ullink'
    pkg.version.gpg.sign = true
}

plugindev {
    pluginId 'com.ullink.msbuild'
    pluginName 'com.ullink.gradle:gradle-msbuild-plugin'
    pluginImplementationClass 'com.ullink.MsbuildPlugin'
    pluginDescription project.description
    pluginLicenses 'Apache-2.0'
    pluginTags 'gradle', 'plugin', 'msbuild', 'c#', '.net'
    authorId 'gluck'
    authorName 'Francois Valdy'
    authorEmail 'francois.valdy@gmail.com'
    projectUrl "https://github.com/Ullink/${project.name}"
    projectInceptionYear '2012'
    done()
}


